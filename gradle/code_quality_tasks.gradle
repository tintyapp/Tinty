/**
 *  Paintroid: An image manipulation application for Android.
 *  Copyright (C) 2010-2015 The Catrobat Team
 *  (<http://developer.catrobat.org/credits>)
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU Affero General Public License as
 *  published by the Free Software Foundation, either version 3 of the
 *  License, or (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 *  GNU Affero General Public License for more details.
 *
 *  You should have received a copy of the GNU Affero General Public License
 *  along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'jacoco-android'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

task checkstyle(type: Checkstyle) {
    configFile file('../config/checkstyle.xml')
    source '.'
    include '**/*.java', '**/*.xml', '**/*.gradle'
    exclude '**/gen/**', '**/build/**', 'libraryProjects/**', '**/.idea/**'

    classpath = files()

    // needed for console output of warnings/errors
    showViolations true
    ignoreFailures false

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task pmd(type: Pmd) {
    ruleSetFiles = files('../config/pmd.xml')
    ruleSets = []

    source '.'
    include '**/*.java'
    exclude '**/gen/**', '**/build/**', '**/res/**'

    ignoreFailures false

    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn 'checkstyle'
check.dependsOn 'pmd'

jacoco {
    toolVersion = "0.8.1"
}

jacocoAndroidUnitTestReport {
    csv.enabled false
    html.enabled true
    xml.enabled true
}


android {
    lintOptions {
        // specific ignores should be defined via lint.xml file
        lintConfig file('../config/lint.xml')

        textReport false
        xmlReport true
        htmlReport true
        xmlOutput file("build/reports/lint-report.xml")
        htmlOutput file("build/reports/lint-report.html")
    }
}

dependencies {
    pmd 'net.sourceforge.pmd:pmd-core:5.8.1'
    pmd 'net.sourceforge.pmd:pmd-java:5.8.1'

    checkstyle 'com.puppycrawl.tools:checkstyle:7.6'
}